/*
The parameterized  initial  configuration  is  expressed  as:   
invalid >= 1, dirty = 0, valid = 0

loop (State (S x) Z Z) t

act a (State (S x) Z Z)
act b (act a (State (S x) Z Z))

The potentially unsafe states:
(1) invalid >= 0, dirty >= 1, valid >= 1
(2) invalid >= 0, dirty >= 2, valid >= 0
*/

action :: RH | RM | WH1 | WH2 | WM;
number :: Z | S number;
state :: State number number number | Stop;
boolean :: True | False;
list $a :: Nil | Cons $a (list $a);

loop (State (S x) Z Z) t

where

loop = %state { 
   %acts {
    case acts of {
      Nil : test state;
      Cons a as : loop (act a state) as;
    }
  }
}

loop1 = %state { 
   %acts {
    case acts of {
      Nil : state;
      Cons a as : loop1 (act a state) as;
    }
  }
}

act = %a {
  %state {
   case a of {
     RH  : rh  state;
     RM  : rm  state;
     WH1 : wh1 state;
     WH2 : wh2 state;
     WM  : wm  state;
   }
  }
}

add = %x {
  %y {
    case y of {
      Z   : x;
      S n : S (add x n);
    }
  }
}

rh = %state {
  case state of {
    State i d v : case d of {
                    Z : case v of {Z : state; S v1: Stop;}; 
                    S d1 : Stop;
                  };
    Stop : Stop;              
  }
}

rm = %state {
  case state of {
    State i d v : case i of { 
                    S i1 : State (add i1 d) Z  (S v);
                    Z : state;
                  };
    Stop : Stop;              
  }
}

wh1 = %state {
  case state of {
    State i d v : case d of { 
                    S d1 : Stop;
                    Z : state;
                  };
    Stop : Stop;              
  }
}

wh2 = %state {
  case state of {
    State i d v : case v of { 
                    S v1 : State (add (add i d) v1) (S Z) Z;
                    Z : state;
                  };
    Stop : Stop;              
  }
}

wm = %state {
  case state of {
    State i d v : case i of { 
                    S i1 : State (add (add i1 d) v) (S Z) Z;
                    Z : state;
                  };
    Stop : Stop;              
  }
}

test = %state {
  case state of {
    State i d v : case d of {
                    S d1 : case d1 of {S d2: False; Z : case v of {S x : False; Z: True;};};
                    Z : True;
                  };
    Stop : True;
  }
}